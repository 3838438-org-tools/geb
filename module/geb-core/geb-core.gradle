import geb.gradle.saucelabs.SauceAccount

apply plugin: "geb-saucelabs"

repositories {
	maven { url "http://repository-saucelabs.forge.cloudbees.com/release" }
}

dependencies {
	compile seleniumDependency, seleniumSupportDependency
	
	compile project(":module:geb-ast")
	compile project(":module:geb-waiting")

	sauceConnect "com.saucelabs:sauce-connect:3.0.28"
}

test {
	maxParallelForks 2
	if (driver == "htmlunit") {
		exclude "**/ExoticAttributeValuesSpec.class"
	}
}

task allCrossBrowserTests {
	dependsOn 'allSauceTests'
}

sauceLabs {
	browsers {
		chrome_linux_34
		delegate."chrome_Windows 7_34"
		delegate."chrome_OS X 10.9_34"
		firefox_linux_28
		delegate."firefox_Windows 7_28"
		delegate."firefox_OS X 10.9_28"
		delegate."internetExplorer_Windows 7_8"
		delegate."internetExplorer_Windows 7_9"
		delegate."internetExplorer_Windows 7_10"
		delegate."internetExplorer_Windows 7_11"
		delegate."safari_OS X 10.9_7"
	}
	task {
		testClassesDir = test.testClassesDir
		testSrcDirs = test.testSrcDirs
		classpath = test.classpath
		maxParallelForks 3
	}
	account {
		username = System.getenv(SauceAccount.USER_ENV_VAR)
		accessKey = System.getenv(SauceAccount.ACCESS_KEY_ENV_VAR)
	}
	connect {
		timeout 15 // minutes
	}
}

modifyPom { pom ->
	pom.project {
		name "Geb Core"
		description "Geb (pronounced \"jeb\") is a browser automation solution. It brings together the power of WebDriver, the elegance of jQuery content selection, the robustness of Page Object modelling and the expressiveness of the Groovy language."
	}
	
	pom.dependencies.findAll { it.groupId == "org.seleniumhq.selenium" }*.scope = "provided"
}