import geb.gradle.browserstack.BrowserStackAccount
import geb.gradle.saucelabs.SauceAccount

apply plugin: "geb-saucelabs"
apply plugin: "geb-browserstack"

repositories {
    maven { url "http://repository-saucelabs.forge.cloudbees.com/release" }
}

dependencies {
    compile seleniumDependency, seleniumSupportDependency

    compile project(":module:geb-ast")
    compile project(":module:geb-waiting")
    compile 'org.jodd:jodd-lagarto:3.7.1'

    sauceConnect "com.saucelabs:ci-sauce:1.128"
}

test {
    def availableProcessors = isCi ? 2 : Runtime.runtime.availableProcessors().intdiv(2)
    maxParallelForks(Math.max(2, availableProcessors))
}

task allCrossBrowserTests {
    dependsOn 'allSauceLabsTests', 'allBrowserStackTests'
}

sauceLabs {
    browsers {
        chrome_linux
        firefox_linux
        safari_mac_7 {
            capabilities platform: "OS X 10.9"
        }
    }
    task {
        testClassesDir = test.testClassesDir
        classpath = test.classpath
        maxParallelForks 5
    }
    account {
        username = System.getenv(SauceAccount.USER_ENV_VAR)
        accessKey = System.getenv(SauceAccount.ACCESS_KEY_ENV_VAR)
    }
    connect {
        timeout 10 // minutes
    }
}

browserStack {
    def applicationAddresses = [8000, 8080, 9000, 9090, 9999].collect { "http://localhost:$it" }
    application(*applicationAddresses)

    browsers {
        htcOne {
            capabilities browserName: "android", platform: "ANDROID", device: "HTC One M8"
        }
        chrome_mac
        chrome_windows
        firefox_windows_47
        firefox_mac_47
        create("internet explorer_windows_8")
        create("internet explorer_windows_9")
        create("internet explorer_windows_10")
        create("internet explorer_windows_11")

    }
    task {
        testClassesDir = test.testClassesDir
        classpath = test.classpath
        maxParallelForks 5
    }
    account {
        username = System.getenv(BrowserStackAccount.USER_ENV_VAR)
        accessKey = System.getenv(BrowserStackAccount.ACCESS_KEY_ENV_VAR)
    }
}

task nonIeBrowserStackTests {
    dependsOn 'htcOneTest', 'chromeMacTest', 'chromeWindowsTest', 'firefoxWindows47Test', 'firefoxMac47Test'
}

task ieBrowserStackTests {
    dependsOn 'internet explorerWindows8Test', 'internet explorerWindows9Test', 'internet explorerWindows10Test', 'internet explorerWindows11Test'
}

modifyPom { pom ->
    pom.project {
        name "Geb Core"
        description "Geb (pronounced \"jeb\") is a browser automation solution. It brings together the power of WebDriver, the elegance of jQuery content selection, the robustness of Page Object modelling and the expressiveness of the Groovy language."
    }

    pom.dependencies.findAll { it.groupId == "org.seleniumhq.selenium" }*.scope = "provided"
}