buildscript {
	repositories {
		mavenRepo urls: "http://gradle.artifactoryonline.com/gradle/plugins"
	}
	dependencies {
		classpath "org.gradle.plugins:gradle-idea-plugin:0.2"
	}
}

def spockDependency = "org.spockframework:spock-core:0.5-groovy-1.6-SNAPSHOT"
def seleniumDependency = "org.seleniumhq.selenium:selenium:2.0a5"
def jettyDependency = "org.mortbay.jetty:jetty:6.1.21"
def groovyDependency = "org.codehaus.groovy:groovy-all:1.6.7"

def groovyModules = ["core", "spock", "test-utils"]
def publishedModules = ["core", "spock"]
if (hasProperty("withSamples")) {
	groovyModules << "google"
}

subprojects {
	apply plugin: "org.gradle.idea"
	
	if (project.name in publishedModules) {
		apply plugin: 'maven'
	}
	
	if (project.name in groovyModules) {
		apply plugin: "groovy"
		apply from: "file:${rootDir}/clover.gradle"
		
		dependencies {
			groovy groovyDependency
		}
		
		compileGroovy.options.fork = false
		compileTestGroovy.options.fork = false
		
		if (project.hasProperty('t')) {
			test.doFirst {
				test.include "**/${t}*.class"
			}
		}
	}
	
	version = '0.4-SNAPSHOT'
	group = 'org.codehaus.geb'
	project.archivesBaseName = "geb-${project.name}"
	
	sourceCompatibility = 1.5
	targetCompatibility = 1.5
	
	repositories {
		mavenCentral()
		mavenRepo name: "spock-snapshots", urls: ["http://m2repo.spockframework.org/snapshots"]
	}
	
	configurations {
		compile.transitive = true
		testCompile.transitive = true
		deployerJars
	}
	
	dependencies {
		deployerJars "org.apache.maven.wagon:wagon-webdav-jackrabbit:1.0-beta-6"
	}
	
	if (hasProperty('codehausUsername') && hasProperty('codehausPassword')) {
		uploadArchives {
			repositories.mavenDeployer {
				configuration = configurations.deployerJars
				repository(url: "dav:https://dav.codehaus.org/repository/geb/") {
					authentication(userName: codehausUsername, password: codehausPassword)
				}
				snapshotRepository(url: "dav:https://dav.codehaus.org/snapshots.repository/geb/") {
					authentication(userName: codehausUsername, password: codehausPassword)
				}
				pom.project {
					licenses {
						license {
							name 'The Apache Software License, Version 2.0'
							url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
							distribution 'repo'
						}
					}
				}
			}
		}
	}
}

project(':core') {
	dependencies {
		compile "org.seleniumhq.selenium:selenium:2.0a5" // this pulls in all the drivers, need to stop that
		testCompile spockDependency
		testCompile seleniumDependency
		testCompile jettyDependency
		testCompile files("${rootDir}/test-utils/build/classes/main") {
			builtBy ':test-utils:compileGroovy'
		}
		testCompile files("${rootDir}/runners/spock/build/classes/main") {
			builtBy ':runners:spock:compileGroovy'
		}
	}
}

project(':runners:spock') {
	dependencies {
		compile spockDependency
		compile seleniumDependency
		compile project(':core')
		testCompile jettyDependency
		testCompile files("${rootDir}/test-utils/build/classes/main") {
			builtBy ':test-utils:compileGroovy'
		}
	}
}

project(':test-utils') {
	dependencies {
		compile jettyDependency
		compile project(':runners:spock')
	}
}

if (hasProperty('withSamples')) {
	project(':samples:google') {
		dependencies {
			compile project(':runners:spock')
		}
		sourceSets {
			test {
				resources {
					fileTree('src/test/resources').include('**/*.groovy')
				}
			}
		}
	}
}

// Root Project 

repositories {
	mavenCentral()
}

configurations {
	build
}

dependencies {
	build "com.cenqua.clover:clover:3.0.2"
	build "org.apache.ant:ant-junit:1.8.1@jar"
	build "org.apache.ant:ant-nodeps:1.8.1@jar"
}

task test(dependsOn: getTasksByName("test", true)) << {
	def reportsDir = "${buildDir}/reports"
	
	// Aggregate the test results
	ant.taskdef(
		name: 'junitreport2', 
		classname: "org.apache.tools.ant.taskdefs.optional.junit.XMLResultAggregator",
		classpath: configurations.build.asPath
	)
	
	def testReportsDir = new File("${reportsDir}/tests")
	if (testReportsDir.exists()) {
		testReportsDir.deleteDir()
	}
	testReportsDir.mkdirs()
	
	ant.junitreport2(todir: testReportsDir) {
		subprojects.each {
			def testResultsDir = "${it.buildDir}/test-results"
			if (new File(testResultsDir).exists()) {
				fileset(dir: testResultsDir) {
					include(name: "TEST-*.xml")
				}
			}
		}
		report(todir: testReportsDir)
	}
	
	// Aggregate the coverage results
	if (project.hasProperty("withClover")) {
		def db = "clover/clover.db"
		def mergedDb = "${buildDir}/${db}"
		def cloverReportsDir = "${reportsDir}/clover"
		ant.taskdef(resource: "cloverlib.xml", classpath: configurations.build.asPath)
		ant."clover-merge"(initstring: mergedDb) {
			subprojects.each {
				if (it.getTasksByName("test", false)) {
					cloverdb(initstring: "${it.buildDir}/${db}")
				}
			}
		}
		ant."clover-report"(initstring: mergedDb) {
			current(outfile:"${cloverReportsDir}/clover.xml")
		}
		ant."clover-html-report"(initstring: mergedDb, outdir:"${cloverReportsDir}/html")
	}
}