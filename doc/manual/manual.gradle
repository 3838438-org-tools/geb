import org.gradle.plugin.pgp.signing.*
buildscript {
	repositories {
		mavenRepo (name: "Alkemist's GitHub", urls: "http://cloud.github.com/downloads/alkemist") {
			pattern = "[organisation]/[module]-[revision].[ext]"
		}
		mavenRepo name: "scala-tools", urls: "http://scala-tools.org/repo-releases"
	}
	dependencies {
		classpath "markdown2book:markdown2book:1.0-SNAPSHOT"
	}
}

archivesBaseName = "geb-manual"
output = file("build/manual")

task compileApi(dependsOn: publishedGroovyModules.collect { project(it).groovydoc }) {
	publishedGroovyModules.collect { project(it) }.each { groovyProject ->
		inputs.dir groovyProject.groovydoc.destinationDir
	
		doLast {
			project.copy {
				from groovyProject.groovydoc.destinationDir
				into "build/manual-api/api/$groovyProject.name"
			}
		}
	}

	outputs.dir "build/manual-api"
}

task compileManual {
	inputs.dir "src"
	outputs.dir "build/manual-manual"

	doLast {
		new markdown2book.Generator(file("src"), file("build/manual-manual"), "UTF-8").generate()
	}
}

task compile(type: Sync, dependsOn: [compileManual, compileApi]) {
	from "build/manual-api"
	from "build/manual-manual"
	into output

	inputs.dir "build/manual-api"
	inputs.dir "build/manual-manual"
	outputs.dir output
}

task zip(type: Zip, dependsOn: compile) {
	inputs.dir output
	outputs.file archivePath
	from output
}

artifacts {
	archives zip
}

// Maven plugin only adds install to java projects
task install(type: Upload, dependsOn: configurations.archives.buildArtifacts) {
	description = "Does a maven install of the archives artifacts into the local .m2 cache."
	repositories {
		mavenInstaller name: "mavenInstaller"
	}
}